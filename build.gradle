plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.bombo'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	// actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// MVC
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Bean Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Session
	implementation 'org.springframework.session:spring-session-core'

	// JDBC API
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'

	// SPRING DATA JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// H2 Database Driver
	runtimeOnly 'com.h2database:h2'

	// Mysql Database Driver
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Spring Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// TEST-CONTAINER
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mysql'

	// Spring Restdocs
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

// RESTDOCS 설정 추가

asciidoctor {
	configurations 'asciidoctorExt'

	baseDirFollowsSourceFile() // 다른 adoc 파일 include 시 경로를 baseDir로 맞춘다.

	inputs.dir snippetsDir

	// 특정 파일만 html로 생성
	sources {
		include("**/index.adoc")
	}

	dependsOn test
	outputDir file('build/docs/asciidoc')
}

task copyAsciidoc(type: Copy) {
	doFirst {
		delete "src/main/resources/static/docs/index.html"
	}
	from "build/docs/asciidoc/"
	into "src/main/resources/static/docs"
	include "index.html"

	dependsOn asciidoctor
}

build {
	dependsOn copyAsciidoc
}